// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id 'com.android.application' version '8.3.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.9.22' apply false
    id 'com.google.devtools.ksp' version '1.9.22-1.0.18' apply false
}

tasks.register('clean', Delete) {
    delete rootProject.buildDir
}

tasks.register('summary') {
    def download = { name, host ->
        println "==========$name summary==========="
        def folder = new File("app/src/$name/assets")
        def fileEtag = new File("$folder/summary.etag")
        def etag = null
        if (fileEtag.exists()) etag = fileEtag.readLines().join('')
        def src = "https://$host/tag/summary.json"
        def url = new URL(src)
        def connection = (HttpURLConnection) url.openConnection()
        try {
            connection.connectTimeout = 1000 * 5
            connection.readTimeout = 1000 * 60
            connection.useCaches = true
            if (etag != null && etag != '') connection.setRequestProperty('if-none-match', etag)
            connection.connect()
            println "${connection.getResponseCode()}: ${connection.getResponseMessage()}"
            if (connection.getResponseCode() == HttpURLConnection.HTTP_NOT_MODIFIED) return
            def fileJson = new File("$folder/summary.json")
            connection.inputStream.withReader { i -> fileJson.withOutputStream { it << i } }
            def newEtag = connection.getHeaderField("ETag")
            println "etag: $etag -> $newEtag"
            if (newEtag != null && newEtag != '') fileEtag.withOutputStream { it << newEtag }
        } catch (Exception e) {
            println e.message
        } finally {
            connection.disconnect()
        }
    }
    doLast {
        download("konachan", "konachan.com")
        download("yande", "yande.re")
        println 'summary'
    }
}

tasks.register('publish') {
    dependsOn 'app:assembleRelease'
    doLast {
        println 'publish'
    }
}