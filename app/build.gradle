plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
//    id 'com.github.ben-manes.versions' version '0.39.0'
}

android {
    def config = new File("local.properties").withInputStream { stream ->
        new Properties().tap { load(stream) }
    }
    compileSdkVersion 33
    buildToolsVersion '33.0.2'

    namespace 'com.github.yueeng.moebooru'
    defaultConfig {
        applicationId "com.github.yueeng.moebooru"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 7
        versionName "1.0.7"
        resourceConfigurations += ['zh']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        kapt.arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
        buildConfigField "String", "BUILD_TIME", "\"${config["buildTime"] ?: new Date().format("YYYYMMddHHmm")}\""
    }

    signingConfigs {
        release {
            storeFile file(config['storeFile'])
            storePassword config['storePassword']
            keyAlias config['keyAlias']
            keyPassword config['keyPassword']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
//            ndk.abiFilters 'armeabi-v7a', 'arm64-v8a'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions 'moe'
    productFlavors {
        yande {
            dimension 'moe'
            applicationIdSuffix '.yande'
            resValue 'string', 'app_name', 'Yande'
            resValue 'string', 'app_host', 'yande.re'
            resValue 'string', 'app_ip', '198.98.54.92'
            resValue 'string', 'app_create_time', '2006-8-13'
            manifestPlaceholders = [host: "yande.re"]
        }
        konachan {
            dimension 'moe'
            applicationIdSuffix '.konachan'
            resValue "string", "app_name", "Konachan"
            resValue 'string', 'app_host', 'konachan.com'
            resValue 'string', 'app_ip', '104.28.10.64'
            resValue 'string', 'app_create_time', '2008-1-13'
            manifestPlaceholders = [host: "konachan.com"]
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin']
        }
    }
    kotlinOptions {
        jvmTarget = "17"
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
//    ndkVersion '23.0.7599858'
}

dependencies {
    def glide_version = '4.15.1'
    def okhttp_version = '4.10.0'
    def retrofit_version = '2.9.0'
    def room_version = '2.5.1'
    def lifecycle_version = '2.6.1'
    def kotlinx_coroutines_version = '1.6.3-native-mt'
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinx_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinx_coroutines_version"
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.1.1'
    implementation 'androidx.activity:activity-ktx:1.7.0'
    implementation 'androidx.fragment:fragment-ktx:1.5.6'
    implementation 'androidx.work:work-runtime-ktx:2.8.1'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-scalars:$retrofit_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    implementation "com.github.bumptech.glide:okhttp3-integration:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation 'com.github.franmontiel:PersistentCookieJar:1.0.1'
    implementation 'jp.wasabeef:glide-transformations:4.3.0'
    implementation 'com.alexvasilkov:gesture-views:2.8.3'
    implementation 'org.jsoup:jsoup:1.15.4'
    implementation 'com.github.yueeng:uCrop:2020.09.28'
    implementation 'org.kohsuke:github-api:1.314'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.7'
}
