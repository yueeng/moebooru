plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.jetbrainsKotlinAndroid)
    alias(libs.plugins.googleDevtoolsKsp)
    alias(libs.plugins.kotlinParcelize)
}

android {
    def config = new File("local.properties").withInputStream { stream ->
        new Properties().tap { load(stream) }
    }
    compileSdk 35
    buildToolsVersion = '35.0.1'

    namespace 'com.github.yueeng.moebooru'
    defaultConfig {
        applicationId "com.github.yueeng.moebooru"
        minSdkVersion 21
        targetSdkVersion 35
        versionCode 9
        versionName "1.0.9.1"
        resourceConfigurations += ['zh']
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
        buildConfigField "String", "BUILD_TIME", "\"${config["buildTime"] ?: new Date().format("YYYYMMddHHmm")}\""
    }

    signingConfigs {
        release {
            storeFile file(config['storeFile'])
            storePassword config['storePassword']
            keyAlias config['keyAlias']
            keyPassword config['keyPassword']
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '.debug'
        }
        release {
//            ndk.abiFilters 'armeabi-v7a', 'arm64-v8a'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions = ['moe']
    productFlavors {
        yande {
            dimension 'moe'
            applicationIdSuffix '.yande'
            resValue 'string', 'app_name', 'Yande'
            resValue 'string', 'app_host', 'yande.re'
            resValue 'string', 'app_ip', '198.251.89.183'
            resValue 'string', 'app_create_time', '2006-8-13'
            manifestPlaceholders.host = "yande.re"
        }
        konachan {
            dimension 'moe'
            applicationIdSuffix '.konachan'
            resValue "string", "app_name", "Konachan"
            resValue 'string', 'app_host', 'konachan.com'
            resValue 'string', 'app_ip', '104.21.4.105'
            resValue 'string', 'app_create_time', '2008-1-13'
            manifestPlaceholders.host = "konachan.com"
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    ksp {
        useKsp2 = false
    }
    packagingOptions {
        resources {
            excludes += ['DebugProbesKt.bin']
        }
    }
}

dependencies {
    implementation libs.kotlinx.coroutines.core
    implementation libs.kotlinx.coroutines.android
    implementation libs.androidx.core.ktx
    implementation libs.androidx.preference.ktx
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.lifecycle.livedata.ktx
    implementation libs.androidx.lifecycle.viewmodel.ktx
    implementation libs.androidx.lifecycle.process
    implementation libs.androidx.swiperefreshlayout
    implementation libs.androidx.paging.runtime.ktx
    implementation libs.androidx.activity.ktx
    implementation libs.androidx.fragment.ktx
    implementation libs.androidx.work.runtime.ktx
    implementation libs.androidx.palette.ktx
    implementation libs.androidx.room.runtime
    ksp libs.androidx.room.compiler
    implementation libs.androidx.room.ktx
    implementation libs.androidx.room.paging
    implementation libs.androidx.material
    implementation libs.androidx.flexbox
    implementation libs.gson
    implementation libs.okhttp
    implementation libs.okhttp.logging.interceptor
    implementation libs.retrofit
    implementation libs.retrofit.converter.gson
    implementation libs.retrofit.converter.scalars
    implementation libs.glide
    implementation libs.glide.okhttp3.integration
    ksp libs.glide.ksp
    implementation libs.glide.transformations
    implementation libs.persistentCookieJar
    implementation libs.gesture.views
    implementation libs.jsoup
    implementation libs.ucrop
    implementation libs.github.api
    testImplementation libs.junit
    androidTestImplementation libs.androidx.test.ext.junit
    androidTestImplementation libs.androidx.test.espresso
}